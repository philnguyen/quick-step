#lang racket/base

(require redex
         racket/set
         racket/match
         racket/contract)

(define-language Î›
  ;; Syntax
  [e ::= v x (e e) (if e e e) (set! x e)]
  [v ::= o b (Î» (x) e)]
  [o ::= add1 integer? procedure?]
  [b ::= integer â„¤]
  [x ::= variable-not-otherwise-mentioned]
  ;; Semantics
  [#|Answer       |# A  ::= V (err string)]
  [#|Value        |# V  ::= o b (clo x e Ï)]
  [#|Kontinuation |# Îº  ::= (Ï† ... Î±â‚–)]
  [#|Kont. Frame  |# Ï†  ::= (ar e Ï) (fn V) (if e e Ï) (set! Î±)]
  [#|Value Address|# Î±  ::= any]
  [#|Kont. Address|# Î±â‚– ::= (e Ï)] ; fixed choice, from P4F
  [#|Environment  |# Ï  ::= (side-condition (name Ï  any) (Ï?  (term Ï )))]
  [#|Value Store  |# Ïƒ  ::= (side-condition (name Ïƒ  any) (Ïƒ?  (term Ïƒ )))]
  [#|Kont. Store  |# Ïƒâ‚– ::= (side-condition (name Ïƒâ‚– any) (Ïƒâ‚–? (term Ïƒâ‚–)))]
  [#|Configuration|# Ï‚  ::= #|function begin |# Î±â‚–
                            #|function return|# (A Î±â‚–)]
  [#|State        |# Î£  ::= (Ï‚ Ïƒ Ïƒâ‚–)])

(define Î±?  (redex-match? Î› Î± ))
(define Î±â‚–? (redex-match? Î› Î±â‚–))
(define V?  (redex-match? Î› V))
(define x?  (redex-match? Î› x))
(define Îº?  (redex-match? Î› Îº))
(define Ï?  (hash/c x?  Î±?         #:flat? #t))
(define Ïƒ?  (hash/c Î±?  (set/c V?) #:flat? #t))
(define Ïƒâ‚–? (hash/c Î±â‚–? (set/c Îº?) #:flat? #t))
(define-term âŠ¥Ï ,(hash))
(define-term âŠ¥Ïƒ ,(hash))

(define-metafunction Î›
  ğ‘° : e -> Î£
  ;; Load initial state
  [(ğ‘° e) (Î±â‚– âŠ¥Ïƒ Ïƒâ‚–)
   (where Î±â‚– (e âŠ¥Ï))
   (where Ïƒâ‚– ,(hash (term Î±â‚–) (set)))])

;; quick-step on (narrow) states (â† âŠ† Î£ Ã— Î£)
(define â†
  (reduction-relation Î› #:domain Î£

   ;; There's no separate stack component (though the stack-store maps addresses to stacks).
   ;; The stack is accumulated and destroyed (or saved to store) during each "quick" step.
   ;; This state space is an abstraction of small-step,
   ;; where there are only 2 classes of states:

   ;; âŸ¨âŸ¨e,ÏâŸ©, Ïƒ, Ïƒâ‚–âŸ© marks the beginning of function body `e` in context `Ï`
   ;; This `âŸ¨e,ÏâŸ©` is also the stack address to return to when done.                      
   [(Î±â‚– Ïƒ Ïƒâ‚–) . --> . Î£
    Eval
    (judgment-holds ((Î±â‚–) Î±â‚– Ïƒ Ïƒâ‚– . â†“â‚‘ . Î£))]

   ;; âŸ¨âŸ¨A,âŸ¨e,ÏâŸ©âŸ©, Ïƒ, Ïƒâ‚–âŸ© marks the finished evaluation of closure body `âŸ¨e,ÏâŸ©` with result `A`,
   ;; about to return to each stack in `Ïƒâ‚–(âŸ¨e,ÏâŸ©)`
   [((A Î±â‚–) Ïƒ Ïƒâ‚–) . --> . Î£
    Kont
    (judgment-holds (Îº . âˆˆ . (@ Ïƒâ‚– Î±â‚–)))
    (judgment-holds (Îº A Ïƒ Ïƒâ‚– . â†“â‚– . Î£))]))

;; Big-step-ish relation that:
;; - When jumping to another Î»'s body, pushes the current stack and "pauses"
;;   at the start of this new body
;; - When done evaluating a Î»'s body, returns the "local" result
(define-judgment-form Î›
  #:contract (Îº Î±â‚– Ïƒ Ïƒâ‚– . â†“â‚‘ . Î£)
  #:mode     (I I  I I  . â†“â‚‘ . O)

  ;; Bases
  
  [(Îº  o    Ïƒ Ïƒâ‚– . â†“â‚– . Î£)
   -------------------------------------------------------------------"E-Op"
   (Îº (o _) Ïƒ Ïƒâ‚– . â†“â‚‘  . Î£)]

  [(Îº  b    Ïƒ Ïƒâ‚– . â†“â‚– . Î£)
   -------------------------------------------------------------------"E-Base"
   (Îº (b _) Ïƒ Ïƒâ‚– . â†“â‚‘ . Î£)]

  [(Îº (clo x e Ï)   Ïƒ Ïƒâ‚– . â†“â‚– . Î£)
   -------------------------------------------------------------------"E-Lam"
   (Îº ((Î» (x) e) Ï) Ïƒ Ïƒâ‚– . â†“â‚‘ . Î£)]

  [(V . âˆˆ . (@ Ïƒ (@ Ï x)))
   (Îº V     Ïƒ Ïƒâ‚– . â†“â‚– . Î£)
   -------------------------------------------------------------------"E-Var"
   (Îº (x Ï) Ïƒ Ïƒâ‚– . â†“â‚‘ . Î£)]

  ;; Pushes
  
  [(((if e_1 e_2 Ï) Ï† ... Î±â‚–) (    e          Ï) Ïƒ Ïƒâ‚– . â†“â‚‘ . Î£)
   -------------------------------------------------------------------"E-If"
   ((               Ï† ... Î±â‚–) ((if e e_1 e_2) Ï) Ïƒ Ïƒâ‚– . â†“â‚‘ . Î£)]

  [(((ar e_2 Ï) Ï† ... Î±â‚–) ( e_1      Ï) Ïƒ Ïƒâ‚– . â†“â‚‘ . Î£)
   -------------------------------------------------------------------"E-App"
   ((           Ï† ... Î±â‚–) ((e_1 e_2) Ï) Ïƒ Ïƒâ‚– . â†“â‚‘ . Î£)]

  [(((set! (@ Ï x)) Ï† ... Î±â‚–) (        e  Ï) Ïƒ Ïƒâ‚– . â†“â‚‘ . Î£)
   -------------------------------------------------------------------"E-Set!"
   ((               Ï† ... Î±â‚–) ((set! x e) Ï) Ïƒ Ïƒâ‚– . â†“â‚‘ . Î£)]
  )

(define-judgment-form Î›
  #:contract (Îº A Ïƒ Ïƒâ‚– . â†“â‚– . Î£)
  #:mode     (I I I I  . â†“â‚– . O)

  [-------------------------------------------------------------------"K-Ret"
   ((Î±â‚–) V Ïƒ Ïƒâ‚– . â†“â‚– . ((V Î±â‚–) Ïƒ Ïƒâ‚–))]

  [(maybe-t? V)
   ((           Ï† ... Î±â‚–) (e Ï) Ïƒ Ïƒâ‚– . â†“â‚‘ . Î£)
   -------------------------------------------------------------------"K-IfT"
   (((if e _ Ï) Ï† ... Î±â‚–) V     Ïƒ Ïƒâ‚– . â†“â‚– . Î£)]

  [(maybe-f? V)
   ((           Ï† ... Î±â‚–) (e Ï) Ïƒ Ïƒâ‚– . â†“â‚‘ . Î£)
   -------------------------------------------------------------------"K-IfF"
   (((if _ e Ï) Ï† ... Î±â‚–) V     Ïƒ Ïƒâ‚– . â†“â‚– . Î£)]

  [(((fn V)   Ï† ... Î±â‚–) (e Ï) Ïƒ Ïƒâ‚– . â†“â‚‘ . Î£)
   -------------------------------------------------------------------"K-AppSwap"
   (((ar e Ï) Ï† ... Î±â‚–) V     Ïƒ Ïƒâ‚– . â†“â‚– . Î£)]

  [(where Î±    x) ; 0CFA
   (where Ï_1  (ext Ï x Î±))
   (where Îº    (Ï† ... Î±â‚–))
   (where Î±â‚–_1 (e Ï_1))
   (where Ïƒ_1  (âŠ” Ïƒ Î± V))
   (where Ïƒâ‚–_1 (âŠ” Ïƒâ‚– Î±â‚–_1 Îº))
   -------------------------------------------------------------------"K-AppClo"
   (((fn (clo x e Ï)) Ï† ... Î±â‚–) V Ïƒ Ïƒâ‚– . â†“â‚– . (Î±â‚–_1 Ïƒ_1 Ïƒâ‚–_1))]

  [((       Ï† ... Î±â‚–) (Î´ o V) Ïƒ Ïƒâ‚– . â†“â‚– . Î£)
   -------------------------------------------------------------------"K-AppOp"
   (((fn o) Ï† ... Î±â‚–) V       Ïƒ Ïƒâ‚– . â†“â‚– . Î£)]

  [-------------------------------------------------------------------"K-AppErr"
   (((fn b) Ï† ... Î±â‚–) _ Ïƒ Ïƒâ‚– . â†“â‚– . ((err ,(format "apply ~a" (term b))) Ïƒ Ïƒâ‚–))]

  [((         Ï† ... Î±â‚–) 1 (âŠ” Ïƒ Î± V) Ïƒâ‚– . â†“â‚– . Î£)
   -------------------------------------------------------------------"K-Set!"
   (((set! Î±) Ï† ... Î±â‚–) V Ïƒ         Ïƒâ‚– . â†“â‚– . Î£)]

  [-------------------------------------------------------------------"K-Err"
   ((_ _ ... Î±â‚–) (err string) Ïƒ Ïƒâ‚– . â†“â‚– . (((err string) Î±â‚–) Ïƒ Ïƒâ‚–))]
  )

(define-metafunction Î›
  Î´ : o V -> A
  [(Î´ add1 b          ) â„¤]
  [(Î´ add1 o          ) (err "add1")]
  [(Î´ add1 (clo _ _ _)) (err "add1")]
  [(Î´ integer? b          ) 1]
  [(Î´ integer? o          ) 0]
  [(Î´ integer? (clo _ _ _)) 0]
  [(Î´ procedure? b          ) 0]
  [(Î´ procedure? o          ) 1]
  [(Î´ procedure? (clo _ _ _)) 1])


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;; Helpers
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; Non-deterministically pick an element in set/list
(define-judgment-form Î›
  #:contract (any . âˆˆ . any)
  #:mode     (O   . âˆˆ . I)
  [(side-condition ,(set? (term any_xs)))
   (where {_ ... any_x _ ...} ,(set->list (term any_xs)))
   -------------------------------------------------------------------"âˆˆ-Set"
   (any_x . âˆˆ . any_xs)]
  [-------------------------------------------------------------------"âˆˆ-List"
   (any_x . âˆˆ . {_ ... any_x _ ...})])

;; Look up in map
(define-metafunction Î›
  @ : any any -> any
  [(@ any_m any_x) ,(hash-ref (term any_m) (term any_x))])

;; Join 2 multimaps
(define-metafunction Î›
  âŠ” : any any any -> any
  [(âŠ” any_m any_x any_y)
   ,(hash-update (term any_m) (term any_x) (Î» (s) (set-add s (term any_y))) set)])

;; Update map
(define-metafunction Î›
  ext : any any any -> any
  [(ext any_m any_x any_y) ,(hash-set (term any_m) (term any_x) (term any_y))])

;; See if `V` is plausibly true (non-0) or false (0)
(define-relation Î›
  maybe-t? âŠ† V
  [(maybe-t? number)
   (side-condition (not (equal? 0 (term number))))]
  [(maybe-t? â„¤)]
  [(maybe-t? o)]
  [(maybe-t? (clo _ _ _))])
(define-relation Î›
  maybe-f? âŠ† V
  [(maybe-f? 0)]
  [(maybe-f? â„¤)])

;; Render state space from running `e`
(define (viz e) (traces â† (term (ğ‘° ,e))))

;; Render derivation tree of first "quick-step"s on `e`
(define-syntax-rule (show-first-step e)
  (let ()
    (define-term Î±â‚–â‚ (e âŠ¥Ï))
    (show-derivations
     (build-derivations
      ((Î±â‚–â‚) Î±â‚–â‚ âŠ¥Ïƒ ,(hash (term Î±â‚–â‚) (set)) . â†“â‚‘ . Î£))
     #:racket-colors? #t)))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;; Tests
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(define-term e1
  ((Î» (x)
     (if (add1 (add1 (add1 x)))
         (set! x 43)
         44))
   42))

;; (viz (term e1))
;; (show-first-step e1)
